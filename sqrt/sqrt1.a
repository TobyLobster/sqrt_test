; https://codebase64.org/doku.php?id=base:fast_sqrt

MLO     = $02
MHI     = $03
THI     = $04
result  = $05

    * = $0200

start
    LDY #$00    ; R = 0
    LDX #$07
    CLC         ; clear bit 16 of M
loop
    TYA
    ORA stab-1,X
    STA THI     ; (R ASL 8) | (D ASL 7)
    LDA MHI
    BCS skip0   ; M >= 65536? then T <= M is always true
    CMP THI
    BCC skip1   ; T <= M
skip0
    SBC THI
    STA MHI     ; M = M - T
    TYA
    ORA stab,x
    TAY         ; R = R OR D
skip1
    ASL MLO
    ROL MHI     ; M = M ASL 1
    DEX
    BNE loop

    ; last iteration

    BCS skip2
    STY THI
    LDA MLO
    CMP #$80
    LDA MHI
    SBC THI
    BCC skip3
skip2
    INY         ; R = R OR D (D is 1 here)
skip3
    RTS
stab
   !byte $01,$02,$04,$08,$10,$20,$40,$80
