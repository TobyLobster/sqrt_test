; from hexwab (https://stardot.org.uk/forums/viewtopic.php?p=367937#p367937)

in_lo     = $02
THI	  = $04
MLO	  = $02
MHI 	  = $03

* = $200

; input in X (high byte) and in_lo (low byte)
; result in A
; bytes: 224
; cycles: best 42, avg 198, worst 220
sqrt14
	txa
	bne lo
do_lo
	lda in_lo
	; fall through
lo
	cmp #8*8
	bcc a0
a1
	cmp #12*12
	bcc a10
a11
	cmp #14*14
	bcc a110
a111
	cmp #15*15
	lda #7
	bne resume
a110
	cmp #13*13
	lda #6
	bne resume
a10
	cmp #10*10
	bcc a100
a101
	cmp #11*11
	lda #5
	bne resume
a100	cmp #9*9
	lda #4
	bne resume
a0
	cmp #4*4
	bcc a00
a01
	cmp #6*6
	bcc a010
a011
	cmp #7*7
	lda #3
	bne resume
a010
	cmp #5*5
	lda #2
	bne resume
a00
	cmp #2*2
	bcc a000
a001
	cmp #3*3
	lda #1
	bne resume
a000
	cmp #1*1
	lda #0
resume
	rol
	cpx #0
	beq done
cont
	tay
	; result should be $pq. we have $p0
	txa
	sec
	sbc sqtab-1,Y
	sta MHI
	;lda in_lo
	;sta MLO
	tya
        asl
        asl
        asl
        asl
	tay ; R in Y

	lda MLO
	 asl
	 rol MHI
	 asl
	 rol MHI
	 asl
	 rol MHI
	 asl
	 rol MHI
	 sta MLO


	 TYA
	 ORA #4
	 STA THI     ; (R ASL 8) | (D ASL 7)
	 LDA MHI
	 BCS .skip00  ; M >= 65536? then T <= M is always true
	 CMP THI
	 BCC .skip10  ; T <= M
.skip00
	 SBC THI
	 STA MHI     ; M = M - T
	 TYA
	 ORA #8
	 TAY         ; R = R OR D
.skip10
	 ASL MLO
	 ROL MHI     ; M = M ASL 1

	 TYA
	 ORA #2
	 STA THI     ; (R ASL 8) | (D ASL 7)
	 LDA MHI
	 BCS .skip01  ; M >= 65536? then T <= M is always true
	 CMP THI
	 BCC .skip11  ; T <= M
.skip01
	 SBC THI
	 ;STA MHI     ; M = M - T
	 ;TYA
	 ;ORA #4
	 ;TAY         ; R = R OR D
	 iny
	 iny
	 iny
	 iny
.skip11
	 ASL MLO
	 ROL; MHI     ; M = M ASL 1

	 ;TYA
	 ;ORA #1
	 ;STA THI     ; (R ASL 8) | (D ASL 7)
	 iny
	 sty THI
	 dey
	 ;LDA MHI
	 BCS .skip02  ; M >= 65536? then T <= M is always true
	 CMP THI
	 BCC .skip12  ; T <= M
.skip02
	 SBC THI
	 STA MHI     ; M = M - T
	 iny
	 iny
.skip12
	 ASL MLO
	 ROL ;MHI     ; M = M ASL 1


	 ; last iteration

	 BCS .skip2
	 STY THI
;	 LDA MLO
;	 CMP #$80
;	 LDA MHI
	 asl MLO
	 SBC THI
	 BCC .skip3
.skip2
	 INY         ; R = R OR D (D is 1 here)
.skip3
	 tya
done
	 RTS

sqtab
    !byte 1,4,9,16,25,36,49,64,81,100,121,144,169,196,225
fourtab
    !byte $ff,$ef,$df,$cf,$bf,$af,$9f,$8f,$7f,$6f,$5f,$4f,$3f,$2f,$1f,$0f