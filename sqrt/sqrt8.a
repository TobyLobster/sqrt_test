; https://mdfs.net/Info/Comp/6502/ProgTips/SqRoot

num = $02
sub = $04

* = $200

sqr                                     ; On entry, !num=input value
    ldx #1:stx sub+0:dex:stx sub+1      ; Initialise sub to first subtrand
                                        ; and initialise X to SQR(0)
sqr_loop                                ; Repeatedly subtract increasing
    sec                                 ;   odd numbers until num<0
    lda num+0:tay:sbc sub+0:sta num+0   ; num=num-subtrand, remainder in Y
    lda num+1:sbc sub+1:sta num+1
    bcc sqr_done                        ; num<0, all done
    inx                                 ;
    lda sub+0:adc #1:sta sub+0          ; step +2 to next odd number
    bcc sqr_loop                        ; no overflow, subtract again
    inc sub+1:bne sqr_loop              ; INC high byte and subtract again
sqr_done
    rts                                 ; X=root, Y=remainder
