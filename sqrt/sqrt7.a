; http://6502org.wikidot.com/software-math-sqrt
; Tweaked by tobylobster to be slightly smaller and faster

numl = $02
numh = $03
root = $04
loop_counter = $05

* = $200

start
    ldx #$ff
    stx loop_counter
    inx
    stx root
    sec
l1  lda numh
    sbc #$40
    tay
    txa
    sbc root
    bcc l2
    sty numh
    !byte $90       ; 'bcc' opcode: never branches, just skips the next instruction ('txa') in 2 cycles
l2  txa
    rol root
    asl numl
    rol numh
    rol
    asl numl
    rol numh
    rol
    tax
    lsr loop_counter
    bne l1
    rts
