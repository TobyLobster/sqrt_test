; http://6502org.wikidot.com/software-math-sqrt
; Tweaked by tobylobster and 0xC0DE to be smaller and faster

numl = $02
numh = $8A                  ; Memory location is opcode for 'txa'
loop_counter = $03
root = $04

* = $200

start
    ldx #$ff
    stx loop_counter
    inx
    stx root
    sec
l1  lda numh
    sbc #$40
    tay
    txa
    sbc root
    bcc l2 + 1              ; Branches to the operand of the next instruction(!), which is 'txa'
l2
    sty numh
    rol root
    asl numl
    rol numh
    rol
    asl numl
    rol numh
    rol
    tax
    lsr loop_counter
    bne l1
    rts
